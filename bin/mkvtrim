#!/usr/bin/env fish

argparse h/help -- $argv
or exit

set -g start $argv[1] # e.g. 00:00:25.375
set -g end $argv[2] # e.g. 00:00:34.458
set -g input $argv[3]
set -g output $argv[4]

set -g usage (path basename (status filename))" START END INPUT OUTPUT"
set -g time_fmt "[[HH:]MM:]SS[.NNN]"

function warn
    echo >&2 -s (set_color --italics red) "$argv" (set_color normal)
end

function valid_time
    string match -rq "(?:\d+:)*\d+(?:\.\d+)" "$argv"
end

function print_help
    echo -s (set_color --bold) "Usage:" (set_color normal) " $usage"
    echo \t"START and END format: $time_fmt"
end

tabs -4

if set -q _flag_help[1]
    print_help
    return 0
else if test (count $argv) -ne 4
    warn "Syntax error!"
    print_help >&2
    return 2
else if not valid_time $start
    warn "Invalid start time! $start"
    echo >&2 \t"Use format: $time_fmt"
    return 2
else if not valid_time $end
    warn "Invalid end time! $end"
    echo >&2 \t"Use format: $time_fmt"
    return 2
else if path is -fv $input
    warn "File not found! "(path resolve $input)
    return 1
end

mkvmerge --output $output --split parts:$start-$end $input
