#!/usr/bin/env fish

set -gx this manpdf

# MANPDF_DIR is the path to the directory where we will save generated PDFs.
# It will be created if it doesn't exist.
if path is -d "$MANPDF_DIR"
    true
else if path is -d "$XDG_DATA_HOME"
    set -gx MANPDF_DIR $XDG_DATA_HOME/doc/pdf
else
    set -gx MANPDF_DIR $HOME/.manpath
end
mkdir -p $MANPDF_DIR; or exit

function man_page_title
    set -l man_file (command man -w $argv)
    or return

    path basename $man_file | string replace -rf '\.([^.]+)(?:\.gz)?$' '.$1'
end

# The first sed call implements these commands, in order:
#   - Set PDF Title metadata from the man page's nicely-formatted title
#     - Based on args to the man(7) `.TH` or mdoc(7) `.Dt` macro
#   - Set PDF Subject metadata to the man page's short description
#     - First try:  The line after the `.SH NAME` man macro
#     - Second try: Args to the `.Nd` mdoc macro
#   - Create 1st-level bookmarks for all 1st-level headings
#     - From all mdoc `.Sh` or man `.SH` macros
#   - Create 2nd-level bookmarks for all 2nd-level headings
#     - From all `.SS` macros
#   - Set PDF options (at top of file):
#     - .pdfview /PageMode /UseOutlines <- Show bookmarks panel when opened
#     -          /Page 1                <- Open to first page
#     -          /View [/Fit]           <- Zoom to fit page to window
#     - .nr PDFOUTLINE.FOLDLEVEL 1      <- Collapse all nested bookmarks
#     - .nr PDFHREF.VIEW.LEADING 30.0p  <- Set bookmark targets 30 points
#                                          above the text so the window won't
#                                          cut it off
#   - Call the `.pdfsync` macro to apply the metadata commands
set -g sed_script '
s#^\.(TH|Dt) "?(\S+)"? "?(\S+)"?#.pdfinfo /Title \L\2(\3)\E\n&#g;
/^\.SH "?NAME"?/{n;s#^[^[:space:]]+ \\?- (.+)$#&\n.pdfinfo \/Subject \1#g;};
s#^\.Nd (.+)$#&\n.pdfinfo /Subject \1#g;
s#^\.S[hH] (.+)$#.pdfbookmark 1 \1\n&#g;
s#^\.SS (.+)$#.pdfbookmark 2 \1\n&#g;
1s#.*#.pdfview /PageMode /UseOutlines /Page 1 /View [/Fit]\n.nr PDFOUTLINE.FOLDLEVEL 1\n.nr PDFHREF.VIEW.LEADING 30.0p\n&#;
$s#.*#&\n.pdfsync#;
'
# The second sed call removes unsightly and unnecessary escape sequences
# from bookmark titles, which aren't present to be removed until the first
# set of commands is complete, and converts arguments to the `.so` (source)
# macro to absolute paths [test case: zshall(1)].
set -g sed_script_2 '
/^\.pdfbookmark/s/\\?f([BIRP]|\[[CRV]?\])//g
/^\.pdfbookmark/s/\\s-?[0-9]//g
/^\.pdfbookmark/s/\\//g
/^\.pdfbookmark/s/\\?\[[ac]q\]/\'/g
s#^\.so ([^/].*)$#.so '(path dirname $man_path)'/../\1#p
'

function process_man_file -a man_path processed_man_path
    # `gunzip -c` sends the decompressed file to standard output, leaving the
    # original file intact; when `-c` is combined with `-f`, gunzip will pass
    # non-gzipped files through unchanged instead of throwing an error.
    gunzip -c -f $man_path \
    | sed -E -f (string trim $sed_script | psub) \
    | sed -E -f (string trim $sed_script_2 | psub) \
    > $processed_man_path
end

function man_to_ps -a man_file ps_file
    # groff options:
    #   -t          = Preprocess with tbl(1) -- needed for terminfo(5) et al.
    #   -Tps        = Selects the PostScript output device
    #   -m mandoc   = Loads the macro that handles man pages
    #   -m pdfmark  = Loads the macro that will process the metadata and
    #                 bookmarks we added earlier
    #   -c          = Disables colour output, because getting ANSI colours
    #                 into a PostScript file is considerably outside the scope
    #                 of this already-ambitious script
    groff -t -Tps -m mandoc -m pdfmark -c $man_file > $ps_file
    or exit

    # It's possible for groff to return 0 while failing to produce any output,
    # so make whether $ps_file is empty the criteria for claiming success.
    test -s $ps_file
end

function ps_to_pdf -a ps_file pdf_file
    # GhostScript options:
    #   -q        = Suppresses startup messages
    #   -dBATCH   = Suppresses other output too ("batch file mode")
    #   -dNOPAUSE = Disables the prompt and pause after each page
    gs -q -dBATCH -dNOPAUSE -sDEVICE=pdfwrite -sOutputFile=$pdf_file $ps_file
end

function main
    argparse -n$this f/force -- $argv
    or return

    if not command -sq gs
        echo >&2 "missing requirement: GhostScript"
        exit 1
    end

    set -f man_path (command man -w $argv)
    or exit

    set -f man_title (man_page_title $man_path)
    set -f pdf_file $MANPDF_DIR/$man_title.pdf

    if path is -v -f $pdf_file; or set -q _flag_force
        if set -f temp_dir (mktemp -d -t $this.XXXXXX)
            # unformatted man page with pdfmark inserts
            set -f man_file $temp_dir/$man_title

            # PostScript file with extra PDF instructions
            set -f ps_file $temp_dir/$man_title.ps
        else
            echo >&2 "cannot continue without temporary directory"
            exit 1
        end

        if not process_man_file $man_path $man_file
            echo >&2 "failed to add macros to $man_file"
            # string trim $sed_script
            # string trim $sed_script_2
            exit 1
        end

        if not man_to_ps $man_file $ps_file
            echo >&2 "failed to generate PostScript file for $title"
            exit 1
        end

        if not ps_to_pdf $ps_file $pdf_file
            echo >&2 "failed to generate PDF from $ps_file"
            exit 1
        end
    end

    echo $pdf_file
end

main $argv
