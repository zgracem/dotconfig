#!/usr/bin/env fish

# Automatically clones `git@github.com:example/repository.git` into the subdir
# ./example/repository, creating ./example first if necessary.

function main
    set -l repo_url $argv[1]

    if string match -rq '^[\w-]+/[\w-]+$' $repo_url
        set -f repo_url git@github.com:$repo_url.git
    end

    set -l repo_dir (string match -rg '(?<=github\.com[:/])([^/]+/.+?)(?=\.git|\Z)' $repo_url)

    if set -q repo_dir[1]
        mkdir -p (path dirname $repo_dir)
        and git clone $repo_url $repo_dir
    else
        echo >&2 "couldn't parse repository from $repo_url" \($repo_dir\)
        return 1
    end
end

main $argv
