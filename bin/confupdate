#!/usr/bin/env fish

function git-sync -a user_dir
    switch $hostname
        case "*.opalstack.com"
            # Regular `/usr/bin/git` is so old it doesn't support the `-C` flag
            scl enable rh-git227 "git -C $user_dir fetch --prune"
            and scl enable rh-git227 "git -C $user_dir merge --ff-only"
        case "*"
            git -C $user_dir fetch --prune
            and git -C $user_dir merge --ff-only
    end
end

function main -d "Update configuration from GitHub"
    switch $hostname
        case Citadel.local Pomona
            echo -s "No need for " (set_color $fish_color_command) (status basename) (set_color normal) " on this host :)"
            return
        case Alyx phosphor.pink
            set -gx USER_CONFIG_DIRS ~/src/**/dot{config,private}
        case "*"
            set -gx USER_CONFIG_DIRS ~/.{config,private}
    end

    for user_dir in (path resolve $USER_CONFIG_DIRS)
        git-sync $user_dir
        or break
    end
end

main $argv
