#!/usr/bin/env osascript

on run argv
	set verbosity to false
	set usage_message to "Usage: trash [-v] FILE [FILE ...]"

	if the length of argv is 0 then
		log usage_message
	else if item 1 of argv is "-v" or item 1 of argv is "--verbose" then
		if the length of argv <= 1 then
			set argv to {}
			log usage_message
		else
			set verbosity to true
			set argv to items 2 thru -1 of argv
		end if
	end if

	set files_to_trash to {}
	set verbose_output to {}

	tell application "Finder"
		repeat with each_item in argv
			if each_item does not start with "/" then
				set each_item to (do shell script "pwd") & "/" & each_item
			end if

			if exists each_item as POSIX file then
				set the_alias to (each_item as POSIX file) as alias
				set the end of files_to_trash to the_alias
				set the end of verbose_output to "Moved `" & (the POSIX path of the_alias) & "` to Trash"
			else
				set the end of verbose_output to "Does not exist: `" & each_item & "`"
			end if
		end repeat

		move files_to_trash to trash
	end tell

	if verbosity is true and length of verbose_output > 0 then
		copy join_list(verbose_output, "\n") to stdout
	end if
end run

on join_list(the_list, the_delimiter)
	set AppleScript's text item delimiters to the_delimiter
	set the_string to the_list as string
	set AppleScript's text item delimiters to ""
	return the_string
end join_list
