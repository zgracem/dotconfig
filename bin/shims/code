#!/usr/bin/env fish
# ----------------------------------------------------------------------------
# Workaround for the "duplicate Dock icon" bug on macOS.
# See <https://github.com/microsoft/vscode/issues/138712> et al.
# ----------------------------------------------------------------------------

# Eat a `-w/--wait` switch, if present.
argparse --ignore-unknown w/wait -- $argv

# Also wait if called as `code-wait`.
# This is because some tools check whether the entire value of $EDITOR/$VISUAL
# is a real executable, and `~/bin/code\ --wait` won't be a valid path. So:
#     set -gx EDITOR ~/bin/code-wait
#     set -gx VISUAL $EDITOR
#     ln -s code $EDITOR
if string match -q -- '*-wait' (status filename)
    set -g _flag_wait 1
end

# If called with any other switches, pass everything to the actual CLI tool.
# This will trigger the bug, but it can't be helped without an obnoxious
# increase in complexity.
for arg in $argv
    switch $arg
        case '-*'
            # Add --wait preference back into the arguments first.
            set -q _flag_wait; and set -p argv --wait
            /usr/local/bin/code $argv
            exit
        case '*'
            # Otherwise, just ensure the file(s) exist, because `open` won't.
            test -e $arg; or touch $arg; or exit
    end
end

# Tell `open` our --wait preference.
set -q _flag_wait; and set -p argv -W

# Et voil√†.
open -b com.microsoft.VSCode $argv
